
#library "ws_fixes"

#include "zcommon.acs"

// ACS fixes for WadSmoosh

#define TNT_TEX_REPLACEMENTS 14
// two arrays; a dict type would be nice :/
str TNT_TEX_SRC[TNT_TEX_REPLACEMENTS] = {
	"SW1GSTON", "SW2GSTON",
	"SKY1",
	"SW1BRN1", "SW2BRN1",
	"SW1SKULL", "SW2SKULL",
	"SW1STARG", "SW2STARG", // same used by plutonia below
	"SLADRIP1", "SLADRIP3",
	"BLODGR1", "BLODGR4",
	"WFALL1"
};
str TNT_TEX_DEST[TNT_TEX_REPLACEMENTS] = {
	"SW1GSTNT", "SW2GSTNT",
	"TNT_SKY1",
	"SW1BRN1T", "SW2BRN1T",
	"SW1SKULT", "SW2SKULT",
	"SW1STARP", "SW2STARP",
	"SLADRPT1", "SLADRPT3",
	"BLODGRT1", "BLODGRT4",
	"TWFALL1"
};

#define PLUT_TEX_REPLACEMENTS 15
str PLUT_TEX_SRC[PLUT_TEX_REPLACEMENTS] = {
	"DBRAIN1", "DBRAIN4",
	"FIREBLU1", "FIREBLU2",
	"SW1SKULL", "SW2SKULL",
	"SKY3",
	"SW1BRN1", "SW2BRN1",
	"SW1STARG", "SW2STARG",
	"WFALL1", "WFALL2",
	"WFALL3", "WFALL4"
};
str PLUT_TEX_DEST[PLUT_TEX_REPLACEMENTS] = {
	"PBRAIN1", "PBRAIN4",
	"FIREPLU1", "FIREPLU2",
	"SW1SKULP", "SW2SKULP",
	"PSKY3",
	"SW1BRN1T", "SW2BRN1T",
	"SW1STARP", "SW2STARP",
	"PWFALL1", "PWFALL2",
	"PWFALL3", "PWFALL4"
};

script "WS_TexFix" ENTER
{
	// replace textures in TNT and Plutonia maps
	if ( GetCVar("ws_finaldoom_texswap") == 1 )
	{
		str s = StrParam(n:PRINTNAME_LEVEL);
		s = StrLeft(s, 3);
		if ( StrIcmp(s, "tn_") == 0 )
		{
			for (int i = 0; i <= TNT_TEX_REPLACEMENTS; i++)
			{
				ReplaceTextures(TNT_TEX_SRC[i], TNT_TEX_DEST[i]);
			}
		}
		else if ( StrIcmp(s, "pl_") == 0 )
		{
			for (i = 0; i < PLUT_TEX_REPLACEMENTS; i++)
			{
				ReplaceTextures(PLUT_TEX_SRC[i], PLUT_TEX_DEST[i]);
			}
		}
	}
	// set legacy sky status and check it regularly
	bool legacySkies = false;
	if ( GetCVar("ws_sky_compat") == 1 )
	{
		SetSky(true);
		legacySkies = true;
	}
	while (1)
	{
		if ( GetCVar("ws_sky_compat") == 1 && !legacySkies )
		{
			SetSky(true);
			legacySkies = true;
		}
		else if ( GetCVar("ws_sky_compat") == 0 && legacySkies )
		{
			SetSky(false);
			legacySkies = false;
		}
		Delay(1);
	}
}

function void SetSky(bool legacy)
{
	int skyNum;
	if ( GetLevelInfo(LEVELINFO_LEVELNUM) <= 11 )
	{
		skyNum = 1;
	}
	else if ( GetLevelInfo(LEVELINFO_LEVELNUM) <= 20 )
	{
		skyNum = 2;
	}
	else
	{
		skyNum = 3;
	}
	str newSky = StrParam(s:"SKY", i:skyNum);
	if ( !legacy )
	{
		newSky = StrParam(s:"R", s:newSky);
	}
	ChangeSky(newSky, "");
}
